# https://www.crockford.com/mckeeman.html

## Basic structure
lines
    line lines

line
    comment '000A'
    statement '000A'
    statement comment '000A'
    space line

comment
    '#' anything

anything
    character
    anything anything

statement
    ""
    "SET" space value space value
    print
    request
    do
    exit
    "WAIT" space value
    "ERROR" space value
    assert
    file
    statement space

## Statements
print
    "PRINT"
    "PRINT" space value

request
    method
    method space value
    method space value space value
    method space value space '(' expression ')' space value
    method space value space '(' expression ')' space value space '(' expression ')'

method
    "REQUEST"
    "GET"
    "POST"
    "PUT"
    "DELETE"
    "HEAD"
    "OPTIONS"
    "TRACE"
    "CONNECT"
    "PATCH"

do
    DO space value space '{' '00A0' lines '}'
    DO space value space loop space '{' '00A0' lines '}'
    DO space value space if space '{' '00A0' lines '}'

loop
    "UNTIL" '(' expression ')'
    "WHILE" '(' expression ')'

if
    "IF" '(' expression ')'
    ifelse
    ifelse if

ifelse
    if space ELSE space '{' '00A0' lines '}'

exit
    "EXIT"
    "EXIT" space value

assert
    "ASSERT" space value
    "ASSERT_SUCCESS"
    "ASSERT_REDIRECT"
    "ASSERT_FAILED"
    "ASSERT_CODE" space value
    "ASSERT_CONTAINS" space value space value
    "ASSERT_ERROR" space value space '{' '00A0' lines '}'

file
    "SCRIPT" space value
    "IMPORT" space value space value
    "EXPORT" space value space value
    "APPEND" space value space value
    "REPORT" space value

## Values

value
    literal
    quoted
    '(' expression ')'
    value value

literal
    character - '0020' - '#' - '{' - '}' - ''' - '"' - '(' - ')'
    literal literal

expression
    '(' expression ')'
    identifiers
    quoted
    operator expression
    expression '00A0'
    '00A0' expression
    expression space
    space expression

identifiers
    identifier
    identifier '.' identifiers
    '.' '.'

identifier
    '_'
    alphanumeric
    alphanumeric '-'
    identifier identifier

alphanumeric
    'a' . 'z'
    'A' . 'Z'
    '0' . '9'
    alphanumeric alphanumeric

quoted
    ''' squoutables '''
    '"' dquoutables '''

squoutables
    squoutable
    squoutable squoutables

squoutable
    '00A0'
    character - '''

dquoutables
    dquoutable
    dquoutable dquoutables

dquoutable
    '00A0'
    character - '"'

character
    '0020' . '007E'
    '00A0' . 'FFFF'

operator
    '+'
    '-'
    '/'
    '*'
    '%'
    '='
    '!'
    '|'
    '&'
    '^'
space
    '0020'
    space space

